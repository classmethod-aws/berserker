// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
		classpath "jp.classmethod.aws:gradle-aws-plugin:0.24"
	}
}

version = currentVersion
group = "jp.classmethod.example"
ext.artifactId = "berserker"
ext.defaultEncoding = "UTF-8"

if (hasProperty('env') == false) { ext.env = 'personal' }
apply from: "env/${env}.gradle"

apply plugin: "java"
apply plugin: "spring-boot"
apply plugin: "jp.classmethod.aws.s3"
apply plugin: "jp.classmethod.aws.cloudformation"
apply plugin: "jp.classmethod.aws.beanstalk"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(AbstractCompile) each {
	it.options.encoding = ext.defaultEncoding
}

// ======== libraries ========
repositories {
	mavenCentral()
	maven { url "http://maven.xet.jp/release" } // for spring-data-mirage
}

dependencies {
	// spring
	compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
	compile "org.springframework.data:spring-data-mirage:$springDataMirageVersion"
	
	// loggers
	compile "org.slf4j:slf4j-api:$slf4jVersion"
	compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
	compile "ch.qos.logback:logback-classic:$logbackVersion"

	// other
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	compile "javax.mail:mail:$javaMailVersion"
	compile "mysql:mysql-connector-java:$mysqlVersion"
	compile "org.flywaydb:flyway-core:$flywayVersion"
	compile "com.amazonaws:aws-java-sdk-sqs:$awsJavaSdkVersion"
	
	// testing
	testCompile "junit:junit:$junitVersion"
	testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
	testCompile "org.mockito:mockito-core:$mockitoCoreVersion"
}

configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.name == "commons-logging") {
			// prefer "jcl-over-slf4j" over "commons-logging":
			details.useTarget "org.slf4j:jcl-over-slf4j:$slf4jVersion"
		}
		if (details.requested.name == "log4j") {
			// prefer "log4j-over-slf4j" over "log4j":
			details.useTarget "org.slf4j:log4j-over-slf4j:$slf4jVersion"
		}
	}
}


//======== docker ========
task createDockerfile(type: Copy, dependsOn: jar) {
	from 'src/main/bundle/Dockerfile'
	into 'build/bundle'
	expand(jarFilename: jar.archiveName, artifactId: project.artifactId)
}

task createBundleResources(type: Copy, dependsOn: bootRepackage) {
	from 'src/main/bundle'
	from jar.archivePath
	into 'build/bundle'
	exclude 'src/main/bundle/Dockerfile'
}

//======== beanstalk bundle ========
task createBundle(type: Zip, dependsOn: [createDockerfile, createBundleResources]) {
	archiveName = jar.archiveName.substring(0, jar.archiveName.length() - 4) + '.zip'
	from 'build/bundle'
}

aws {
	profileName = berserker.aws.profileName
	region = berserker.aws.region
}

task uploadBundle(type: jp.classmethod.aws.gradle.elasticbeanstalk.AWSElasticBeanstalkUploadBundleTask, dependsOn: createBundle) {
	file project.createBundle.archivePath
	overwrite project.version.endsWith('-SNAPSHOT')
}

//======== cfn ========
cloudFormation {
	stackName = berserker.aws.cfn.stackName
	conventionMapping.stackParams = {
		def stackParams = berserker.aws.cfn.stackParams
		stackParams['BerserkerVersionLabel'] = getBerserkerVersion()
		stackParams["SolutionStack"] = beanstalk.latestSolutionStackName("64bit Amazon Linux", "Docker")
		getLogger().info("stackParams = {}", stackParams)
		return stackParams
	}
	capabilityIam true
	templateFile = file('src/main/cloudformation/berserker.template')
	templateBucket = berserker.aws.cfn.templateBucket
	templateKeyPrefix = 'berserker'
}

awsCfnMigrateStack.mustRunAfter uploadBundle
awsCfnMigrateStack.dependsOn awsCfnUploadTemplate
awsCfnWaitStackComplete.loopTimeout = 10800 // = 3hr
awsCfnWaitStackDeleted.loopTimeout = 10800 // = 3hr

task awsFullDeploy(dependsOn: [
	uploadBundle,
	awsCfnUploadTemplate,
	awsCfnMigrateStackAndWaitCompleted
]) {
	group 'AWS'
	description 'Deploys full-stack berserker environment to AWS'
}

task awsFullUndeploy(dependsOn: awsCfnDeleteStackAndWaitCompleted) {
	group 'AWS'
	description 'Undeploys full-stack berserker environment from AWS'
}


// ======== wrapper ========
task wrapper(type: Wrapper) {
	gradleVersion = "2.12"
}


//======== environment ========
tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
	project.getSystemProperties().each { key, value ->
		systemProperty key, value
	}
}

Map<String, String> getSystemProperties() {
	def prop = [
		'JDBC_CONNECTION_STRING':  berserker.local.database.connectionString,
		'DB_USERNAME':             berserker.local.database.user,
		'DB_PASSWORD':             berserker.local.database.password,
		'spring.profiles.active':  berserker.local.springProfile,
	]
	if (file(berserker.local.logbackConfigurationFile).exists()) {
		prop.put('logging.config', berserker.local.logbackConfigurationFile)
	}
	return prop
}

String getBerserkerVersion() {
	return uploadBundle.didWork ? uploadBundle.versionLabel : cloudFormation.getStackParameterValue("BerserkerVersion")
}
